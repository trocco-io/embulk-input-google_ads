/*
 * A helper artifact to have the Google Ads client with some transitive dependencies relocated.
 *
 * This helper JAR is built separate from each plugin main JAR so that an impact of package relocation is
 * closed within the helper JAR, not to impact the plugin main JAR.
 */

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"

repositories {
    mavenCentral()
}

group = "io.trocco"
version = "${rootProject.version}"
description = "A helper library for Google Ads for embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    shadow {
        resolutionStrategy.activateDependencyLocking()
        transitive = false
    }
}

dependencies {
    compile("com.google.api-ads:google-ads:30.0.0") {
        exclude group: "commons-logging", module: "commons-logging"
    }

    // Use jcl-over-slf4j instead of commons-logging.
    compile("org.slf4j:jcl-over-slf4j:1.7.12") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
}

// Relocate Guava and Jackson packages since they are incompatible from Embulk's.
shadowJar {
    mergeServiceFiles()
    relocate "com.google.common", "embulk.google-ads.com.google.common"
    relocate "com.google.thirdparty.publicsuffix", "embulk.google-ads.com.google.thirdparty.publicsuffix"
    relocate "com.fasterxml.jackson", "embulk.google-ads.com.fasterxml.jackson"
}

// https://github.com/google/guava/issues/6612#issuecomment-1614992368
sourceSets.all {
  configurations.getByName(runtimeClasspathConfigurationName) {
    attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
  }
  configurations.getByName(compileClasspathConfigurationName) {
    attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
  }
}
