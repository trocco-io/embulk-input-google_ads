plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.0.0" apply false
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "com.palantir.git-version" version "0.13.0"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "io.trocco"
description = "embulk input plugin for Google Ads"
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

def embulkVersion = '0.10.43'

dependencies {
    compileOnly("org.embulk:embulk-api:$embulkVersion") { exclude group: "org.slf4j", module: "slf4j-api" }
    compileOnly("org.embulk:embulk-spi:$embulkVersion") { exclude group: "org.slf4j", module: "slf4j-api" }
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile "org.embulk:embulk-util-config:0.3.3"
    compile "org.embulk:embulk-util-json:0.2.2"
    compile "com.google.guava:guava:31.1-jre"
    compile "org.slf4j:slf4j-log4j12:1.7.36"
    compile project(path: ":shadow-google-ads-helper", configuration: "shadow")

    testCompile "junit:junit:4.13"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile("org.embulk:embulk-core:$embulkVersion") { exclude group: "org.slf4j", module: "slf4j-api" }
    testCompile("org.embulk:embulk-deps:$embulkVersion") { exclude group: "org.slf4j", module: "slf4j-api" }
    testCompile("org.embulk:embulk-junit4:$embulkVersion") { exclude group: "org.slf4j", module: "slf4j-api" }
}

embulkPlugin {
    mainClass = "org.embulk.input.google_ads.GoogleAdsInputPlugin"
    category = "input"
    type = "google_ads"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}


checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE")  // Optional -- if you need other files in the gem.
    authors = ["giwa"]
    email = ["ugw.gi.world@gmail.com"]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Google Ads input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-output-bigquery_java"
    licenses = ["Apache-2.0"]
}

gemPush {
    host = "https://rubygems.org"
}
